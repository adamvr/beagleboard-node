@title = @title ? 'Untitled'
@fields = ['Periods', 'Amplitude', 'Frequency', 'Phase']
html ->
    head ->
        title "#{@title} | Osco"
        script src: '/javascript/jquery.js'
        #script src: '/javascript/jquery.flot.js'
        script src: '/javascript/processing.js'
        script src: '/socket.io/socket.io.js'
        coffeescript ->
            graphDataSize = 50
            graphData = new Array graphDataSize

            sketchProc = (p) ->
                p.frameRate 10
                p.size 600, 250

                rangeY = 256
                centerY = p.height / 2
                scaleY = p.height / rangeY
                stepX = p.width / (graphDataSize - 1)
                lastY = centerY

                p.draw = () ->
                    p.background 224
                    p.stroke 25
                    p.strokeWeight 1
                    p.line 0, centerY, p.width, centerY

                    p.stroke 0
                    p.strokeWeight 3
                    lastX = 0
                    nextX = lastX + stepX
                    for point in graphData
                        nextY = centerY - (point * scaleY)
                        p.line lastX, lastY, nextX, nextY
                        lastX += stepX
                        nextX += stepX
                        lastY = nextY
                    return

            $().ready ->
                canvas = $('#processing')[0]
                processing = new Processing canvas, sketchProc

                socket = io.connect '/osco'
                socket.on 'data', (data) =>
                    for i in [0...graphDataSize]
                        graphData[i] = if data[i] >= 128 then data[i] - 256 else data[i] 

    body ->
        h1 @title
        canvas id: 'processing'

